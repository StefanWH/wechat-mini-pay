buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'//添加Maven发布插件
configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

group 'so.sao'
version '1.0.4-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "https://nexus.sao.so/repository/maven-public/"
        credentials {
            username nexusUserName
            password nexusPwd
        }
    }
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework:spring-web:4.3.7.RELEASE"
    compile "org.springframework.boot:spring-boot-configuration-processor"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.8.7'
    compile 'com.thoughtworks.xstream:xstream:1.4.10'
    compile "org.slf4j:slf4j-api:1.7.22"
    compile "org.bouncycastle:bcprov-jdk15on:1.55"
    compile "com.google.guava:guava:22.0"
    compile "org.apache.httpcomponents:httpclient:4.5.3"
    compile "org.apache.httpcomponents:httpcore:4.4.6"
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile "org.apache.commons:commons-lang3:3.6"
}

compileJava.dependsOn(processResources)

// 打包
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
// 如果是发行版本，则上传到发行库。否则上传到快照库。
def postfixUrl = "repository/maven-snapshots"
if (project.version.endsWith("-RELEASE")) {
    postfixUrl = "repository/maven-releases"
}
// 发布到本地Maven库和远程Maven服务器
publishing {
    repositories {
        mavenLocal()
        maven {
            credentials {
                username nexusUserName
                password nexusPwd
            }
            url "https://nexus.sao.so/$postfixUrl/"
        }
    }
}
